const std = @import("std");
const builtin = @import("builtin");

const manifest = @import("build.zig.zon");
const obs = @import("obs");

pub const std_options: std.Options = .{
    .log_level = if (builtin.mode == .Debug) .debug else .info,
    .logFn = @import("./logging.zig").logFn,
};

const default_locale = "en-US";

var obs_module_pointer: ?*obs.obs_module_t = null;

export fn obs_module_set_pointer(module: ?*obs.obs_module_t) void {
    obs_module_pointer = module;
}

pub fn obsCurrentModule() ?*obs.obs_module_t {
    return obs_module_pointer;
}

export fn obs_module_ver() u32 {
    return obs.LIBOBS_API_VER;
}

var obs_module_lookup: ?*obs.lookup_t = null;

pub fn obsModuleText(val: [*:0]const u8) [*c]const u8 {
    var out: [*c]const u8 = val;
    _ = obs.text_lookup_getstr(obs_module_lookup, val, &out);
    return out;
}

export fn obs_module_get_string(val: [*c]const u8, out: [*c][*c]const u8) bool {
    return obs.text_lookup_getstr(obs_module_lookup, val, out);
}

export fn obs_module_set_locale(locale: [*c]const u8) void {
    if (obs_module_lookup != null)
        obs.text_lookup_destroy(obs_module_lookup);

    obs_module_lookup = obs.obs_module_load_locale(obsCurrentModule(), default_locale, locale);
}

export fn obs_module_free_locale() void {
    obs.text_lookup_destroy(obs_module_lookup);
    obs_module_lookup = null;
}

export fn obs_module_load() bool {
    std.log.info("plugin loaded successfully (version {s})", .{manifest.version});
    std.log.info("{s}", .{obsModuleText("HelloWorld")});
    return true;
}

export fn obs_module_unload() void {
    std.log.info("plugin unloaded", .{});
}

export fn obs_module_author() [*:0]const u8 {
    return "Your Name";
}

export fn obs_module_name() [*:0]const u8 {
    return @tagName(manifest.name);
}

export fn obs_module_description() [*:0]const u8 {
    return "OBS plugin written in Zig";
}
